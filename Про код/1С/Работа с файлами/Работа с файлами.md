#1с #ТектовыеФайлы #РаботаСФайлами
### /// Как записать текстовый файл в 1с 8.3, 8.2
~~~&НаКлиенте
Процедура КакЗаписатьТекстовыйФайл(Команда)
 
    КаталогДляЭкспериментов = КаталогДокументов() + "\helpme1s.ru";
    СоздатьКаталог(КаталогДляЭкспериментов);
 
    ТекстовыйФайл = Новый ТекстовыйДокумент;
 
    // Устанавливаем содержимое файла.
    ТекстовыйФайл.УстановитьТекст("
    |Как прекрасен
    |этот мир!");
 
    // Очищаем содержимое файла.
    ТекстовыйФайл.Очистить();
 
    // Добавляем первую ...
    ТекстовыйФайл.ДобавитьСтроку("Первая строчка файла");
    // ... и вторую строчку файла.
    ТекстовыйФайл.ДобавитьСтроку("Вторая строчка файла");
 
    // Меняем первую строчку на "Привет"
    ТекстовыйФайл.ЗаменитьСтроку(
        1, // номер строки в файле, нумеруется с 1
        "Привет"
    );
 
    // Вставляем новую строчку сразу после первой,
    // то есть на место второй.
    ТекстовыйФайл.ВставитьСтроку(
        2, // номер строки, на место которой вставляем новую
        "Владимир"
    );
 
    // Удаляем вторую строчку ("Владимир")
    ТекстовыйФайл.УдалитьСтроку(2);
 
    // Записываем файл на диск.
    // Если такой файл уже существует - он будет перезаписан.
    ТекстовыйФайл.Записать(
        КаталогДляЭкспериментов + "\out.txt", // путь для сохранения
        КодировкаТекста.UTF8, // кодировка
        Символы.ВК + Символы.ПС // разделитель строк
    );
 
    // Открываем созданный файл в блокноте.
    ЗапуститьПриложение(КаталогДляЭкспериментов + "\out.txt");
 
КонецПроцедуры 
~~~
 
### /// Как прочитать текстовый файл в 1с 8.3, 8.2
 
~~~&НаКлиенте
Процедура КакПрочитатьТекстовыйФайл(Команда)
 
    ОчиститьСообщения();
 
    КаталогДляЭкспериментов = КаталогДокументов() + "\helpme1s.ru";
    СоздатьКаталог(КаталогДляЭкспериментов);
 
    ПутьКФайлу = КаталогДляЭкспериментов + "\in.txt";
 
    // Создаем случайный текстовый файл.
    Служебный.СоздатьПроизвольныйТекстовыйФайл(ПутьКФайлу);
 
    ТекстовыйФайл = Новый ТекстовыйДокумент;
 
    ТекстовыйФайл.Прочитать(
        ПутьКФайлу,
        // Если не указать кодировку напрямую, то она
        // по возможности определится автоматически.
        КодировкаТекста.UTF8, 
        // К этом значению будут приведены разделители
        // строк из файла. Если ничего не указывать -
        // останутся родные.
        Символы.ВК + Символы.ПС
    );
 
    // Выводим весь текст целиком.
    Сообщить(ТекстовыйФайл.ПолучитьТекст());
 
    // Выводим текст по строкам.
    Для Индекс = 1 По ТекстовыйФайл.КоличествоСтрок() Цикл
        Сообщить(ТекстовыйФайл.ПолучитьСтроку(Индекс));
    КонецЦикла;
 
КонецПроцедуры
~~~